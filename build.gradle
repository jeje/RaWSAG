import groovyx.net.http.HTTPBuilder

import static groovyx.net.http.ContentType.JSON
import static groovyx.net.http.Method.GET
import static groovyx.net.http.Method.POST

apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'wrapper'

ext.sourceCompatibility = JavaVersion.VERSION_1_7

apply from: file('gradle/idea.gradle')
apply from: file('gradle/jacoco.gradle')
apply from: file('gradle/pmd.gradle')
apply from: file('gradle/findbugs.gradle')
apply from: file('gradle/checkstyle.gradle')
//apply from: file('gradle/bintray.gradle')

buildscript {
    repositories {
        maven {
            url 'http://jcenter.bintray.com'
        }
    }
    dependencies {
        repositories {
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
        classpath 'com.jimdo.gradle:gradle-apt-plugin:0.4-SNAPSHOT'
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    }
}

allprojects {
    group = 'com.kalixia.grapi'
    version = '0.4.4-SNAPSHOT'

//    apply from: 'https://raw.github.com/brunodecarvalho/gradle-plugins/master/jacoco-multiproject-aggregator.gradle'
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    // Bintray integration
    task createBintrayPackage << {
        def http = getHttpBuilder()
        http.request(GET, JSON) {
            uri.path = '/packages/' + bintray_subject + '/' + bintray_repo + '/' + bintray_package
            response.'404' = {
                http = getHttpBuilder()
                http.request(POST, JSON) {
                    uri.path = '/packages/' + bintray_subject + '/' + bintray_repo
                    body = [name: bintray_package, desc: 'auto', desc_url: 'auto', labels: ['grapi', 'rest api', 'generator', 'apt']]

                    response.success = { resp ->
                        println 'Created!!!'
                    }
                }
            }
        }
    }
    //NOTE: Please use java 1.7 to avoid http://bugs.sun.com/bugdatabase/view_bug.do?bug_id=6459815
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "${bintray_api_base_url}/maven/${bintray_subject}/${bintray_repo}/${bintray_package}") {
                    authentication(userName: bintray_username, password: bintray_api_key)
                }
            }
        }
    }
    uploadArchives.dependsOn createBintrayPackage
}

def getHttpBuilder() {
    def http = new HTTPBuilder(bintray_api_base_url)
    http.auth.basic bintray_username, bintray_api_key
    return http
}
